tag coord model is vec3;
tag coord world is vec3;
tag coord camera is vec3;
tag coord rot<`t : vec3> is vec3; // Rotated by 90 degrees counterclockwise around the z-axis as an example
tag norm<`t : vec> is `t;
tag coord hom<`t : vec3> is vec4;

declare canon norm<`t> normalize<`t : vec>(`t v);
declare vec4 vec4(vec3 v, float f);
declare vec3 vec3(vec4 v);
declare float dot<`t : vec>(norm<`t> v1, norm<`t> v2);

canon hom<`t> homify<`t : vec3>(`t v) {
    return vec4(v as vec3, 1.) as hom<`t>;
}

canon hom<`t> homNorm<`t : vec3>(norm<`t> v) {
    return vec4(v as vec3, 0.) as hom<`t>;
}

canon `t hom_reduce<`t : vec3>(hom<`t> v) {
    return vec3(v as vec4) as `t;
}

`t->`u hom_matrix_reduce<`t : vec3, `u : vec3>(hom<`t>->hom<`u> m) {
    `t->hom<`t> asHomFlat = [[1., 0., 0.], [0., 1., 0.], [0., 0., 1.], [0., 0., 0.]];
    hom<`u>->`u homReduce = [[1., 0., 0., 0.], [0., 1., 0., 0.], [0., 0., 1., 0.]];
    return homReduce * m * asHomFlat;
}

canon rot<`t> rotate<`t : vec3>(`t v) {
    `t->rot<`t> rotation_matrix = [[0., -1., 0.], [1., 0., 0.], [0., 0., 1.]];
    return rotation_matrix * v;
}

`t->rot<`u> rotate_matrix<`t : vec3, `u : vec3>(`t->`u m) {
    `u->rot<`u> rotation_matrix = [[0., -1., 0.], [1., 0., 0.], [0., 0., 1.]];
    return rotation_matrix * m;
}

canon `t rot_reduce<`t : vec3>(rot<rot<rot<rot<`t>>>> v) {
    return v as `t;
}

void main() {
    model uPosition = [1., 1., 1.];
    norm<model> uNorm = [0., 0., 1.];
    hom<model>->hom<world> uModel = [[1., .5, 0., 0.], [0., 1., 0., 0.], [1., 0., 1., 0.], [0., 0., .5, 1.]];
    hom<world>->hom<camera> uWorld = [[1., .5, 0., 0.], [0., 1., 0., 0.], [1., 0., 1., 0.], [0., 0., .5, 1.]];
    world lightPos = [0., 1., 0.];
    
    float angle = dot(uPosition in norm<model>, uNorm);
    auto worldHomPosition = uPosition in hom<world>;
    auto worldPosition = worldHomPosition in world;
    float camAngle = dot(uNorm in norm<camera>, lightPos in norm<camera>);

    print angle;
    print worldHomPosition;
    print worldPosition;
    print camAngle;

    auto rrm = uPosition in rot<rot<model>>;
    auto rm = rrm in rot<model>; // Must loop around using 'rotate'
    model->rot<world> mtwr = rotate_matrix<model, world>(hom_matrix_reduce<model, world>(uModel)); // Can't synthesize this (yet?)

    print rm;
    print mtwr;
}